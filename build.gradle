plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.10.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'ch.hevs.cloudio'
version = '0.2.0-SNAPSHOT'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.10.1'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: '2.10.1'
    compile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'
    compile group: 'org.mapdb', name: 'mapdb', version: '3.0.7'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }

        mavenSources(MavenPublication) {
            from components.java
            artifact sourcesJar
        }

        mavenJavadoc(MavenPublication) {
            from components.java
            artifact javadocJar
        }
    }
}

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_KEY"
    configurations = ['archives']
    pkg {
        repo = 'clients'
        name = project.group + ':' + project.name
        userOrg = 'cloudio-project'
        desc= 'cloud.iO Java endpoint library/client'
        websiteUrl = 'http://cloudio.hevs.ch'
        issueTrackerUrl = 'https://github.com/cloudio-project'
        vcsUrl = 'https://github.com/cloudio-project'
        licenses = ['MIT']
        version {
            name = project.version
        }
    }
    publications = ['mavenJava', 'mavenSources', 'mavenJavadoc']
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "$System.env.BINTRAY_USER"
            password = "$System.env.BINTRAY_KEY"
            maven = true
        }

        defaults {
            publications('mavenJava', 'mavenSources', 'mavenJavadoc')
            publishPom = true
        }
    }
}
